#!perl6

use v6;

use CGI;
use HTML::Template;
use Digest::MD5;

class Wiki {

    my  $.content_path       is rw;
    # RAKUDO: For some reason, the addition of a second class attribute makes
    # rakudo hang. Haven't been able to find out why. A simple -e program with
    # two class attrs runs fine.
#    my  $.modifications_path is rw;
    my $.template_path       is rw;
    my $.userfile_path       is rw;

    has $.cgi                is rw;
    has %.sessions;

    method init {
        # a rakudo bug prevents us from setting the attribute
        # outside of a method
        $.content_path = 'data/articles/';
#        $.modifications_path = 'data/modifications/';
        $.template_path = 'skin/';
        $.userfile_path = 'data/users';
    }

    method handle_request(CGI $cgi) {
        $.cgi = $cgi;
        my $action = $cgi.param<action> // 'view';

        # RAKUDO: 'when' doesn't work properly yet
        my $handled = False;
        given $action {
            when 'view' { self.view_page(); $handled = True }
            when 'edit' { self.edit_page(); $handled = True }
            when 'log_in'  { self.log_in();  $handled = True }
            when 'log_out' { self.log_out(); $handled = True }
        }

        if !$handled {
            self.not_found();
        }
    }

    method view_page() {
        my $page = $.cgi.param<page> // 'Main_Page';

        if !self.exists_wiki_page($page) {
            my $template = HTML::Template.new(
                filename => $.template_path ~ 'not_found.tmpl');

            $template.param('PAGE' => $page);

            print #status_not_found(),
                  $template.output();
            return;
        }

        my $template = HTML::Template.new(
            filename => $.template_path ~ 'view.tmpl');

        $template.param('TITLE' => $page);
        $template.param('CONTENT' => self.format_html(self.read_page($page)));

        print $template.output();
    }

    method edit_page() {
        my $page = $.cgi.param<page> // 'Main_Page';

        my $session_id = $cgi.cookie('session_id');
        if !$session_id || !exists %.sessions{$session_id} {
            return self.not_authorized($cgi);
        }

        my $already_exists
                        = self.exists_wiki_page($page);
        my $action      = $already_exists ?? 'Editing' !! 'Creating';
        my $old_content = $already_exists ?? self.read_page($page) !! '';
        my $title = "$action $page";

        my $template = HTML::Template.new(
            filename => $.template_path ~ 'edit.tmpl');

        $template.param('PAGE'    => $page);
        $template.param('TITLE'   => $title);
        $template.param('CONTENT' => $old_content);

        print #status_ok(),
              $template.output();
    }

    method not_authorized() {

        my $template = HTML::Template.new(
            filename => $.template_path ~ 'action_not_authorized.tmpl');

        $template.param(DISALLOWED_ACTION => 'edit pages');

        print #status_ok(),
              $template.output();

        return;
    }

    method exists_wiki_page($page) {
        # RAKUDO: use :e
        my $exists = False;
        try {
            my $fh = open( $.content_path ~ $page );
            $exists = True;
        }
        return $exists;
    }

    method read_page_history($page) {

        my $file = $.content_path ~ $page;
        my $exists = False;
        try {
            my $fh = open( $file );
            $exists = True;
        }
        return [] unless $exists;
        my $page_history = eval( slurp($file) );
        return $page_history;
    }

    method read_page($page) {

        my $page_history = self.read_page_history($page);
        return "" unless $page_history;
        my $latest_change = self.read_modification( $page_history.shift );
        return $latest_change[1];
    }

    method read_modification($modification_id) {

        # RAKUDO: Had to inline this class variable because it wouldn't work
        # otherwise.
        my $file = 'data/modifications/' ~ $modification_id;
        # RAKUDO: use :e
        my $exists = False;
        try {
            my $fh = open( $file );
            $exists = True;
        }
        return [] unless $exists;
        return eval( slurp($file) );
    }

    method read_users {
        # RAKUDO: use :e
        my $exists = False;
        try {
            my $fh = open( $.userfile_path );
            $exists = True;
        }
        return {} unless $exists;
        return eval( slurp( $.userfile_path ) );
    }

    method quote($metachar) {
        # RAKUDO: Tried chained trinary. Saw the result and cried.
        return '&#039;' if $metachar eq '\'';
        return '&lt;'   if $metachar eq '<';
        return '&gt;'   if $metachar eq '>';
        return '&amp;'  if $metachar eq '&';
        return $metachar;
    }

    method format_html($text is rw) {
        # RAKUDO: When 'gather' is implemented, we can to this with
        # `join '', gather { ... }` instead.
        my $result = '';
        if $text ~~ Wiki::Syntax::TOP {
            # RAKDUO: Must match again. [perl #57858]
            $text ~~ Wiki::Syntax::TOP;
            for $/<paragraph> -> $p {

                $result ~= '<p>';
                for $p<parchunk> {
                    my $text = $_.values[0];
                    given $_.keys[0] {
                        when 'twext'     { $result ~= $text }
                        when 'wikimark'  { my $page = substr($text, 2, -2);
                                           $result ~= self.make_link($page) }
                        when 'metachar'  { $result ~= self.quote($text) }
                        when 'malformed' { $result ~= $text }
                    }
                 }
                 $result ~= "</p>\n";
            }
        }
        return $result;
    }

    method make_link($page) {
        if self.exists_wiki_page($page) {
            return $.cgi.a({'href' => "?page=$page"}, $page);
        } 
        return $.cgi.a({'href'  => "?action=edit&page=$page",
                        'class' => 'new'}, $page);
    }

    method not_found() {
        say $.cgi.header,
            $.cgi.start_html('Action Not found'),
            $.cgi.h1('Action Not found'),
            $.cgi.end_html;
    }

    method log_in {

        if my $user_name = $.cgi.param<user_name> {

            my $password = $.cgi.param<password>;

            my %users = self.read_users();

            if Digest::MD5::md5_base64(
                   Digest::MD5::md5_base64($user_name) ~ $password
#               ) eq %users{$user_name}<password> {
               ) eq 'carlwhatever' {

                my $template = HTML::Template.new(
                    filename => $.template_path ~ 'login_succeeded.tmpl');

                my $session_id = Digest::MD5::md5_base64(time);
                my $session_cookie = $.cgi.cookie(
                    {
                        '-name'    => 'session_id',
                        '-value'   => $session_id,
                        '-expires' => '+1h'
                    }
                );

                %.sessions{$session_id} = {
                    'user_name' => $user_name,
                };

                print # "HTTP/1.0 200 OK\r\n",
#                      $.cgi.header( '-cookie' => $session_cookie ),
                      $template.output();

                return;
            }

            my $template = HTML::Template.new(
                filename => $.template_path ~ 'login_failed.tmpl');

            print #status_ok(),
                  $template.output();

            return;
        }

        my $template = HTML::Template.new(
            filename => $.template_path ~ 'log_in.tmpl');

        print #status_ok(),
              $template.output();

        return;
    }

    method log_out {

        if defined $.cgi.cookie('session_id') {
            my $template = HTML::Template.new(
                filename => $.template_path ~ 'logout_succeeded.tmpl');

            my $session_id = $.cgi.cookie('session_id');
            # RAKUDO: No 'delete' builtin
#            delete %.sessions{$session_id};
            %.sessions{$session_id} = "DELETED";

            my $session_cookie = $.cgi.cookie(
                {
                    '-name'    => 'session_id',
                    '-value'   => '',
                }
            );

            print #"HTTP/1.0 200 OK\r\n",
#                  $.cgi.header( '-cookie' => $session_cookie ),
                  $template.output();

            return;
        }

        my $template = HTML::Template.new(
            filename => $.template_path ~ 'logout_succeeded.tmpl');

        print #status_ok(),
              $template.output();

        return;
    }
}

grammar Wiki::Syntax {
    token TOP { ^ [ <paragraph> | <newlines> ]* $ };

    token paragraph { <parchunk>+ };
    token newlines { \n ** {2..*} };

    token parchunk { <twext> || <wikimark> || <metachar> || <malformed> };

    # RAKUDO: a token may not be called 'text' [perl #57864]
    token twext { [ <alnum> || <otherchar> || <sp> ]+ };

    # RAKUDO: sequences of quoted characters don't work [perl #57870
#    token otherchar { <[ '!'..'%' '('..'/' ':' ';' '?' '@'
#                      '\' '^'..'`' '{'..'~' ]> };
    token otherchar {
      [ '!' | '#' | '$' | '%' | '(' | ')' | '*' | '+'
      | ',' | '-' | '.' | '/' | ':' | ';' | '=' | '?'
      | '@' | '^' | '_' | '`' | '{' | '|' | '}' | '~'
      | '\\'
      ]
    };

    token sp { ' ' };

    token wikimark { '[[' <twext> ']]' };

    token metachar { '<' || '>' || '&' || \' };

    token malformed { '[' || ']' }
}

my Wiki $wiki = Wiki.new;
$wiki.init();
my $cgi = CGI.new does HTML; # mr. Ugly
$cgi.init();
$wiki.handle_request($cgi);
