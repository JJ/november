#!perl6

use CGI;

class Wiki {

    my  $.content_path       is rw;
    # RAKUDO: For some reason, the addition of a second class attribute makes
    # rakudo hang. Haven't been able to find out why. A simple -e program with
    # two class attrs runs fine.
#    my  $.modifications_path is rw;
    has $.cgi                is rw;

    method init {
        # a rakudo bug prevents us from setting the attribute
        # outside of a method
        $.content_path = 'data/articles/';
#        $.modifications_path = 'data/modifications/';
    }

    method handle_request(CGI $cgi) {
        $.cgi = $cgi;
        my $action = $cgi.param<action> // 'view';

        # RAKUDO: 'when' doesn't work properly yet
        my $handled = False;
        given $action {
            when 'view' { self.view_page(); $handled = True }
            when 'edit' { self.edit_page(); $handled = True }
        }

        if !$handled {
            self.not_found();
        }
    }

    method view_page() {
        my $page = $.cgi.param<page> // 'Main_Page';

        if !self.exists_wiki_page($page) {
            my $title = $page ~ ' not found';
            print $.cgi.header,
                  $.cgi.start_html($page ~ ' not found'),
                  $.cgi.h1($page),
                  $.cgi.a({ 'href' => "?action=edit&page=$page" }, "Create"),
                  $.cgi.p,
                 "The page $page does not exist.",
                 $.cgi.end_html;
            return;
        }

        print $.cgi.header,
              $.cgi.start_html($page),
              $.cgi.h1($page),
              $.cgi.a({'href' => "?action=edit&page=$page"}, "Edit"),
              $.cgi.p,
              self.format_html(self.read_page($page)),
              $.cgi.end_html;
    }

    method edit_page() {
        # $page should be an instance variable?
        my $page = $.cgi.param<page> // 'Main_Page';
        my $text = '';
        try {
            $text = self.read_page($page);
        }
        print $.cgi.header,
              $.cgi.start_html($page),
              $.cgi.h1("Editing $page"),
              $.cgi.start_form({}),
              $.cgi.textarea({cols => 50, rows => 10}, $text),
              $.cgi.submit(<>),
              $.cgi.end_form,
              $.cgi.end_html;
    }

    method exists_wiki_page($page) {
        # RAKUDO: use :e
        my $exists = False;
        try {
            my $fh = open( $.content_path ~ $page );
            $exists = True;
        }
        return $exists;
    }

    method read_page_history($page) {

        my $file = $.content_path ~ $page;
        my $exists = False;
        try {
            my $fh = open( $file );
            $exists = True;
        }
        return [] unless $exists;
        my $page_history = eval( slurp($file) );
        return $page_history;
    }

    method read_page($page) {

        my $page_history = self.read_page_history($page);
        return "" unless $page_history;
        my $latest_change = self.read_modification( $page_history.shift );
        return $latest_change[1];
    }

    method read_modification($modification_id) {

        # RAKUDO: Had to inline this class variable because it wouldn't work
        # otherwise.
        my $file = 'data/modifications/' ~ $modification_id;
        # RAKUDO: use :e
        my $exists = False;
        try {
            my $fh = open( $file );
            $exists = True;
        }
        return [] unless $exists;
        return eval( slurp($file) );
    }

    method quote($metachar) {
        # RAKUDO: Tried chained trinary. Saw the result and cried.
        return '&#039;' if $metachar eq '\'';
        return '&lt;'   if $metachar eq '<';
        return '&gt;'   if $metachar eq '>';
        return '&amp;'  if $metachar eq '&';
        return $metachar;
    }

    method format_html($text is rw) {
        # RAKUDO: When 'gather' is implemented, we can to this with
        # `join '', gather { ... }` instead.
        my $result = '';
        if $text ~~ Wiki::Syntax::TOP {
            # RAKDUO: Must match again. [perl #57858]
            $text ~~ Wiki::Syntax::TOP;
            for $/<paragraph> -> $p {

                $result ~= '<p>';
                for $p<parchunk> {
                    my $text = $_.values[0];
                    given $_.keys[0] {
                        when 'twext'     { $result ~= $text }
                        when 'wikimark'  { my $page = substr($text, 2, -2);
                                           $result ~= self.make_link($page) }
                        when 'metachar'  { $result ~= self.quote($text) }
                        when 'malformed' { $result ~= $text }
                    }
                 }
                 $result ~= "</p>\n";
            }
        }
        return $result;
    }

    method make_link($page) {
        if self.exists_wiki_page($page) {
            return $.cgi.a({'href' => "?page=$page"}, $page);
        } 
        return $.cgi.a({'href'  => "?action=edit&page=$page",
                        'class' => 'new'}, $page);
    }

    method not_found() {
        say $.cgi.header,
            $.cgi.start_html('Action Not found'),
            $.cgi.h1('Action Not found'),
            $.cgi.end_html;
    }
}

grammar Wiki::Syntax {
    token TOP { ^ [ <paragraph> | <newlines> ]* $ };

    token paragraph { <parchunk>+ };
    token newlines { \n ** {2..*} };

    token parchunk { <twext> || <wikimark> || <metachar> || <malformed> };

    # RAKUDO: a token may not be called 'text' [perl #57864]
    token twext { [ <alnum> || <otherchar> || <sp> ]+ };

    # RAKUDO: sequences of quoted characters don't work [perl #57870
#    token otherchar { <[ '!'..'%' '('..'/' ':' ';' '?' '@'
#                      '\' '^'..'`' '{'..'~' ]> };
    token otherchar {
      [ '!' | '#' | '$' | '%' | '(' | ')' | '*' | '+'
      | ',' | '-' | '.' | '/' | ':' | ';' | '=' | '?'
      | '@' | '^' | '_' | '`' | '{' | '|' | '}' | '~'
      | '\\'
      ]
    };

    token sp { ' ' };

    token wikimark { '[[' <twext> ']]' };

    token metachar { '<' || '>' || '&' || \' };

    token malformed { '[' || ']' }
}

my Wiki $wiki = Wiki.new;
$wiki.init();
my $cgi = CGI.new does HTML; # mr. Ugly
$cgi.init();
$wiki.handle_request($cgi);
